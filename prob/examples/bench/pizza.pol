secret:  
  int s_in_school_type = 4;
  int s_birth_year = 1983;
  int s_address_lat = 39003178;
  int s_address_long = -76958199;

belief:
  pif 4:20 then
    int s_in_school_type = uniform 1 1;
    int s_birth_year = uniform 1998 2002
  else pif 3:16 then
    int s_in_school_type = uniform 2 2;
    int s_birth_year = uniform 1990 1998
  else pif 2:13 then
    int s_in_school_type = uniform 3 3;
    int s_birth_year = uniform 1985 1992
  else pif 1:11 then
    int s_in_school_type = uniform 4 4;
    int s_birth_year = uniform 1980 1985
  else
    int s_in_school_type = uniform 0 0;
    int s_birth_year = uniform 1900 1985
  endpif endpif endpif endpif;
  int s_address_lat = uniform 38867884 39103178;
  int s_address_long = uniform -77058199 -76825926

policy:	max_prob_output [s_in_school_type s_birth_year s_address_lat s_address_long] 999/1

querydef pizza -> out :
  #define age = 2010 - s_birth_year in
  #define lr_lat = 38967884 in
  #define ul_lat = 39003178 in
  #define lr_long = -76958199 in
  #define ul_long = -76925926 in

  bool in_school;
  if s_in_school_type >= 4 then
    in_school = 1
  else
    in_school = 0
  endif;

  bool age_criteria;
  if age >= 18 and age <= 28 then
    age_criteria = 1
  else
    age_criteria = 0
  endif;

  bool in_box;
  if s_address_lat <= ul_lat and
     s_address_lat >= lr_lat and
     s_address_long >= lr_long and
     s_address_long <= ul_long
  then
    in_box = 1
  else
    in_box = 0
  endif;

  bool out;
  if (in_school == 1 or age_criteria == 1)
     and in_box == 1 then
    out = 1
  else
    out = 0
  endif

query pizza: skip
